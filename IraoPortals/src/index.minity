namespace irao_portals

tag iraoPortalMarker
tag placedPortal
tag iraoPortal

function load #minecraft:load {
  /tellraw @a [{"text": "[IraoPortals]: yellow"}]
}

function loop #minecraft:tick {
  /recipe take @a irao_portals:iraoportalrecipe
  /advancement revoke @a only irao_portals:iraoportalrecipeadv
  
  for @e.iraoPortalMarker!placedPortal {
    place_portal()
  }
}

macro area_feeler(?max_x, ?max_y) {
  var $blockcount = 0
  var $blockcount2 = 0

  var $col = 0
  down 1 east 1 {
    repeat west 1 {
      $col++
      var $row = 0
      repeat up 1 {
        if (air) {
          $blockcount++
          // setblock stone
        }
        south 1 {
          unless (air) {
            $blockcount2++
            // setblock stone
          }
        }
        $row++
      } while $row < ?max_y
    } while $col < ?max_x
  }

  var $expected_tot = 0
  $expected_tot += ?max_x
  $expected_tot *= ?max_y

  if ($blockcount == $expected_tot) and if ($blockcount2 == $expected_tot) {
    resolve()
  } else {
    reject()
  }
}

macro summon_painting(?motive="minecraft:kebab") {
  var $facingdir
  $facingdir = @s::Facing

  if ($facingdir == 2) {
    summon ~ ~ ~ painting{
      Motive: ?motive,
      Facing: 2b,
      Tags:["{.iraoPortal}"]
    }
  } else {
    if ($facingdir == 3) {
      summon ~ ~ ~ painting{
        Motive: ?motive,
        Facing: 0b,
        Tags:["{.iraoPortal}"]
      }
    } else {
      if ($facingdir == 4) {
        summon ~ ~ ~ painting{
          Motive: ?motive,
          Facing: 1b,
          Tags:["{.iraoPortal}"]
        }
      } else {
        if ($facingdir == 5) {
          summon ~ ~ ~ painting{
            Motive: ?motive,
            Facing: 3b,
            Tags:["{.iraoPortal}"]
          }
        } else {
          /kill @s
        }
      }
    }
  }
}

function place_portal() {
  say Placing portal

  // /tp @s ~ ~-227 ~

  east 1 down 1 {
    when area_feeler(4, 4) then {
      west 1 up 1 {
        summon_painting("minecraft:burning_skull")
      }
    } catch {
      when area_feeler(4, 3) then {
        west 1 up 1 {
          summon_painting("minecraft:donkey_kong")
        }
      } catch {
        up 1 {
          when area_feeler(4, 2) then {
            west 1 {
              summon_painting("minecraft:fighters")
            }
          } catch {
            west 1 {
              when area_feeler(2, 2) then {
                summon_painting("minecraft:void")
              } catch {
                when area_feeler(2, 1) then {
                  summon_painting("minecraft:creebet")
                } catch {
                  when area_feeler(1, 2) then {
                    summon_painting("minecraft:wanderer")
                  } catch {
                    summon_painting("minecraft:aztec2")
                  }
                }
              }
            }
          }
        }
      }
    }
  }

  tag @s placedPortal
  say Placed portal
}

// Recipes
function recipe() {
  give @p glow_item_frame{
    display: {
      Name: json <span color=aqua italic=true bold=true>Irao Portal</span>,
    },
    EntityTag: {
      Silent:1b,
      Invulnerable:1b,
      Invisible:1b,
      Fixed:1b,
      Tags:["{.iraoPortalMarker}"],
      Item: {
        id:"minecraft:glow_item_frame",
        Count:1b,
        Name: json <span color=aqua italic=true bold=true>Irao Portal</span>,
      }
    }
  }
  /clear @s minecraft:knowledge_book 1
}
