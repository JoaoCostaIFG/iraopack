namespace irao_portals

tag iraoPortal
tag placedPortal
tag iraoPortalMarker

score id
score posicaox
score posicaoy
score posicaoz

// Recipes
function recipe() {
  give @p painting{
    display: {
      Name: json <span color=aqua bold=true>Irao Portal</span>,
    },
    EntityTag: {
      Tags:["{.iraoPortal}"],
    }
  }
  /clear @s minecraft:knowledge_book 1
}

function place_portal() {
  say Placing portal
  tag @s placedPortal

  var $rotx
  $rotx = @s::Rotation[0]
  var $roty
  $roty = @s::Rotation[1]

  // save portal info
  summon (forward 0.5) marker{
    // do nothing
  } then {
    tag @s iraoPortalMarker
    @s->id = -1
    forward 1 {
      /tp @s ~ ~ ~
      @s->posicaox = @s::Pos[0]
      @s->posicaoy = @s::Pos[1]
      @s->posicaoz = @s::Pos[2]
    }
    /tp @s ~ ~ ~
    @s::Rotation[0] = 1f * $rotx
    @s::Rotation[1] = 1f * $roty
  }

  say Placed portal
}

function destroy_portal() {
  unless @painting.iraoPortal[distance < 1] {
    say Portal died
    // kill painting drop
    /kill @e[type=minecraft:item,nbt={Item:{id:"minecraft:painting"}},sort=nearest,limit=1]
    // drop item
    summon (forward 0.5 downward 0.5) item{
      Item: {
        id: "minecraft:painting",
        Count: 1b,
        tag: {
          display: {
            Name: json <span color=aqua bold=true>Irao Portal</span>,
          },
          EntityTag: {
            Tags:["{.iraoPortal}"],
          }
        }
      }
    }
    /kill @s
  }
}

function teleport_player() {
  var $myid
  $myid = @s->id

  // for each neaby player
  for @p[distance <= 1] {
    say looking for pair teleporter
    /tag @s add TeleportPlayer

    // loop through all paintings (loaded chunks)
    as(furthest @marker.iraoPortalMarker[distance > 1]) {
      // if the ids match, we teleport
      if ($myid == @s->id) {
        var $posx
        $posx = @s->posicaox
        var $posy
        $posy = @s->posicaoy
        var $posz
        $posz = @s->posicaoz


        summon marker{
          Tags:["TeleportTarget"],
        } then {
          @s::Pos[0] = 1d * $posx
          @s::Pos[1] = 1d * $posy
          @s::Pos[2] = 1d * $posz
          /tp @a[tag=TeleportPlayer, limit=1] @s
          /tag @a[tag=TeleportPlayer, limit=1] remove TeleportPlayer
          /kill @s
        }
      }
    }
  }
}

// Load
function load #minecraft:load {
  /tellraw @a [{"text": "[IraoPortals]: yellow"}]
}

// Tick
function loop #minecraft:tick {
  /recipe take @a irao_portals:iraoportalrecipe
  /advancement revoke @a only irao_portals:iraoportalrecipeadv
  
  for @painting.iraoPortal!placedPortal {
    place_portal()
  }

  for @marker.iraoPortalMarker {
    destroy_portal()
    teleport_player()
  }
}
